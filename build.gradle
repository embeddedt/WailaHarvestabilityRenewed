buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: "com.matthewprenger.cursegradle"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    maven { url "https://maven.bai.lol" }
    maven {
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

minecraft {
    mappings channel: "official", version: minecraft_version

    runs {
        client {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            workingDirectory project.file('run')
            mods {
                wailaharvestability {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            mods {
                wailaharvestability {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            args '--mod', 'wailaharvestability', '--all', '--output', file("src/generated/resources/"), '--existing', file("src/main/resources/")
            mods {
                wailaharvestability {
                    source sourceSets.main
                }
            }
        }
    }
}

group = project.projectDir.name.toLowerCase()
def modId = project.projectDir.name
archivesBaseName = modId + "-mc" + "${minecraft_version}"

sourceSets.main.java.srcDirs += 'java'
sourceSets.main.java.srcDirs += 'apis'
sourceSets.main.resources.srcDirs += 'resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("mcp.mobius.waila:wthit:forge-${wthit_version}")

    implementation fg.deobf("slimeknights.mantle:Mantle:1.16.5-1.6.154")
    implementation fg.deobf("slimeknights.tconstruct:TConstruct:1.16.5-3.3.4.336")

    runtimeOnly fg.deobf("curse.maven:lazydfu-460819:3249059")
}

jar {
    manifest {
        attributes([
            "Implementation-Version": "${version}"
        ])
    }
}

curseforge {
    if (System.getenv("CURSEFORGE_TOKEN") != null) {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        project {
            id = "738662"
            changelog = '[Changelog is not currently available]'
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion "Forge"
            addGameVersion minecraft_version
        }
    }
}